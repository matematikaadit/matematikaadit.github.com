---
layout: "post"
title: "Hanya Pekerjaan Sihir Biasa di Haskell"
tags: "haskell"
---

Sebelum melakukan pekerjaan sihir ini, kita definisikan pragma berikut
terlebih dahulu.

    > {-# LANGUAGE TypeSynonymInstances, FlexibleInstances #-}

Pragma diatas kita gunakan agar kita bisa jadikan `String` yang merupakan
alias untuk `[Char]` sebagai instance suatu kelas (menggunakan Language
pragma `TypeSynonymInstances`), serta mendefinisikan instance untuk fungsi
`String -> a` dan `String` (yang merupakan alias untuk `[Char]`)
(menggunakan Language pragma `FlexibleInstances`).

----------

Pertanyaan hari ini, bisakah kita punya kode seperti ini di Haskell?

    > tigaKata :: String
    > tigaKata = gabung "hanya" "pekerjaan" "sihir"
    >
    > empatKata :: String
    > empatKata = gabung "hanya" "pekerjaan" "sihir" "biasa"

Di fungsi yang pertama gabung punya tiga parameter, sedangkan di fungsi
kedua dia punya empat paramter. Emang bisa punya fungsi yang kayak gitu?
Tentu saja bisa dong! :)

Gini caranya

    > class Gabungan a where
    >      simpan :: [String] -> a
    >      gabung :: a
    >
    >      gabung = simpan []

Nah, itu yang diatas adalah trik utamanya. Sekarang kita kotori tangan kita
dengan trik dibawah ini.

    > instance Gabungan String where
    >     simpan = unwords
    >
    > instance Gabungan a => Gabungan (String -> a) where
    >     simpan xs = \x -> simpan (xs ++ [x])

Sekarang salin dan tempel postingan ini ke file `Gabung.lhs` dan muat di
`ghci` dengan mengetikkan `ghci Gabung.lhs`.

    λ: tigaKata
    "hanya pekerjaan sihir"
    λ: empatKata
    "hanya pekerjaan sihir biasa"
    λ: gabung "Keren" "bukan!" :: String
    "Keren bukan!"

